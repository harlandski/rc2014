10 REM Boggle clone for RC2014
20 REM Set up index, dice$ arrays and declare roll$
30 DIM INDEX(15)
40 FOR I = 0 TO 15
50 READ INDEX(I)
60 NEXT
70 DIM DICE$(15,5)
80 FOR I = 0 TO 15
90 FOR J = 0 TO 5
100 READ DICE$(I,J)
110 NEXT J
120 NEXT I
123 REM GOSUB 4000
125 REM GOSUB 4050
130 DIM ROLL$(15)
200 REM Welcome message
210 CLS
220 PRINT "***********************"
230 PRINT "*                     *"
240 PRINT "*     G I G G L E     *"
250 PRINT "*                     *"
260 PRINT "***********************"
300 REM Set random seed
310 OUT 128,22
320 PRINT "Press any key to start!"
330 COUNT = -1
340 IF (INP(128) AND 1) = 0 THEN COUNT = COUNT -1: GOTO 340
350 X = INP(129)
360 OUT 128,150
370 X= RND (COUNT)
375 REM PRINT COUNT
400 REM Wait message
410 CLS
420 PRINT "***********************"
430 PRINT "*                     *"
440 PRINT "*    L O A D I N G    *"
450 PRINT "*                     *"
460 PRINT "***********************"
500 REM Shuffle index
510 FOR I = 0 to 15
520 R=INT(RND(1)*16)
530 A=INDEX(I)
540 B=INDEX(R)
550 INDEX(I)=B
560 INDEX(R)=A
570 NEXT
575 REM GOSUB 4000
600 REM Populate roll$ arrays
610 FOR I = 0 TO 15
620 R=INT(RND(1)*6)
630 D=INDEX(I)
640 ROLL$(I)=DICE$(D,R)
650 NEXT
655 REM GOSUB 4150
700 REM DISPLAY BOARD
710 CLS
720 FOR I = 0 to 15
730 PRINT ROLL$(I); "  ";
735 REM It seems modulo is not implemented
740 IF (I+1)/4 = INT((I+1)/4) THEN PRINT: PRINT
750 NEXT
800 REM COUNTDOWN
810 PRINT "TIME:     ";
820 FOR I = 180 to 0 STEP -1
830 IF I>= 10 THEN PRINT CHR$(8);CHR$(8);CHR$(8);CHR$(8);
840 IF I<10 THEN PRINT CHR$(8);CHR$(8);CHR$(8);
850 PRINT I;
860 FOR J = 0 to 2100
870 NEXT J
880 NEXT I
900 REM Plat again?
910 PRINT: PRINT: PRINT "Play again (y/n)";
920 INPUT p$
930 IF p$="y" THEN 400
3990 END
4000 REM Print index
4010 FOR I = 0 TO 15
4020 PRINT INDEX(I)
4030 NEXT
4040 RETURN
4050 REM Print dice
4060 FOR I = 0 TO 15
4070 FOR J = 0 TO 5
4080 PRINT DICE$(I,J)
4090 NEXT J
4100 NEXT I
4110 RETURN
4150 REM Print roll$
4160 FOR I = 0 TO 15
4170 PRINT I, ROLL$(I)
4180 NEXT
4190 RETURN
5000 DATA 0,1,2,3,4,5,6,7,8,9,10,11,12,13,14,15
5010 DATA G,E,Y,U,L,K
5020 DATA L,C,R,S,A,E
5030 DATA M,P,D,C,E,A
5040 DATA A,A,O,I,C,T
5050 DATA E,L,U,P,S,T
5060 DATA F,X,R,B,I,O
5070 DATA I,T,A,Y,B,L
5080 DATA A,H,O,S,R,M
5090 DATA Z,N,V,D,A,E
5100 DATA F,E,H,I,Y,E
5110 DATA D,W,N,S,E,O
5120 DATA V,E,T,I,G,N
5130 DATA G,L,R,W,U,I
5140 DATA S,E,H,I,N,P
5150 DATA M,A,Q,O,J,B
5160 DATA T,K,O,N,U,D
